<!-- START doctoc generated TOC please keep comment here to allow auto update -->
<!-- DON'T EDIT THIS SECTION, INSTEAD RE-RUN doctoc TO UPDATE -->
**Table of Contents**  *generated with [DocToc](https://github.com/thlorenz/doctoc)*

- [Day 4 - S3 æœ€å°æ¥å…¥ + IRSA](#day-4---s3-%E6%9C%80%E5%B0%8F%E6%8E%A5%E5%85%A5--irsa)
  - [ç›®æ ‡](#%E7%9B%AE%E6%A0%87)
  - [Step 1/4ï¼šTerraform â€” S3 æ¡¶ + IRSAï¼ˆæœ€å°æƒé™åˆ°å‰ç¼€ï¼‰](#step-14terraform--s3-%E6%A1%B6--irsa%E6%9C%80%E5%B0%8F%E6%9D%83%E9%99%90%E5%88%B0%E5%89%8D%E7%BC%80)
    - [éœ€è¦è½å®çš„ä¿®æ”¹ï¼ˆæœ€å°å¿…éœ€é›†ï¼‰](#%E9%9C%80%E8%A6%81%E8%90%BD%E5%AE%9E%E7%9A%84%E4%BF%AE%E6%94%B9%E6%9C%80%E5%B0%8F%E5%BF%85%E9%9C%80%E9%9B%86)
      - [S3 Bucketï¼ˆé»˜è®¤å®‰å…¨åŸºçº¿ï¼‰](#s3-bucket%E9%BB%98%E8%AE%A4%E5%AE%89%E5%85%A8%E5%9F%BA%E7%BA%BF)
      - [IAM Policyï¼ˆä»…å…è®¸è¯¥å‰ç¼€ï¼‰](#iam-policy%E4%BB%85%E5%85%81%E8%AE%B8%E8%AF%A5%E5%89%8D%E7%BC%80)
      - [IRSA Roleï¼ˆä¿¡ä»» EKS OIDCï¼Œä»…ç»‘å®šåˆ°åº”ç”¨çº§ SAï¼‰](#irsa-role%E4%BF%A1%E4%BB%BB-eks-oidc%E4%BB%85%E7%BB%91%E5%AE%9A%E5%88%B0%E5%BA%94%E7%94%A8%E7%BA%A7-sa)
    - [å…·ä½“çš„ HCL æ–‡ä»¶æ”¹åŠ¨](#%E5%85%B7%E4%BD%93%E7%9A%84-hcl-%E6%96%87%E4%BB%B6%E6%94%B9%E5%8A%A8)
      - [ä¿®æ”¹ `infra/aws/main.tf` æ–‡ä»¶](#%E4%BF%AE%E6%94%B9-infraawsmaintf-%E6%96%87%E4%BB%B6)
      - [ä¿®æ”¹ `infra/aws/outputs.tf` æ–‡ä»¶](#%E4%BF%AE%E6%94%B9-infraawsoutputstf-%E6%96%87%E4%BB%B6)
      - [ä¿®æ”¹ `infra/aws/terraform.tfvars` æ–‡ä»¶](#%E4%BF%AE%E6%94%B9-infraawsterraformtfvars-%E6%96%87%E4%BB%B6)
      - [ä¿®æ”¹ `infra/aws/variables.tf` æ–‡ä»¶](#%E4%BF%AE%E6%94%B9-infraawsvariablestf-%E6%96%87%E4%BB%B6)
      - [æ–°å¢ `infra/aws/modules/app_irsa_s3/main.tf` æ–‡ä»¶](#%E6%96%B0%E5%A2%9E-infraawsmodulesapp_irsa_s3maintf-%E6%96%87%E4%BB%B6)
      - [æ–°å¢ `infra/aws/modules/app_irsa_s3/outputs.tf` æ–‡ä»¶](#%E6%96%B0%E5%A2%9E-infraawsmodulesapp_irsa_s3outputstf-%E6%96%87%E4%BB%B6)
      - [æ–°å¢ `infra/aws/modules/app_irsa_s3/variables.tf` æ–‡ä»¶](#%E6%96%B0%E5%A2%9E-infraawsmodulesapp_irsa_s3variablestf-%E6%96%87%E4%BB%B6)
      - [æ–°å¢ `infra/aws/modules/app_irsa_s3/versions.tf` æ–‡ä»¶](#%E6%96%B0%E5%A2%9E-infraawsmodulesapp_irsa_s3versionstf-%E6%96%87%E4%BB%B6)
    - [Terraform è¾“å‡ºï¼ˆä¾›è„šæœ¬ä¸ K8s ä½¿ç”¨ï¼‰](#terraform-%E8%BE%93%E5%87%BA%E4%BE%9B%E8%84%9A%E6%9C%AC%E4%B8%8E-k8s-%E4%BD%BF%E7%94%A8)
    - [å¿«é€Ÿè‡ªæ£€ï¼ˆCLI å³æ—¶æ ¡éªŒï¼‰](#%E5%BF%AB%E9%80%9F%E8%87%AA%E6%A3%80cli-%E5%8D%B3%E6%97%B6%E6%A0%A1%E9%AA%8C)
  - [Step 2/4 â€” ç»™ SA åŠ  IRSA æ³¨è§£ + æ³¨å…¥ S3 å˜é‡ + å›æ»šæ›´æ–°ï¼ˆä¸æ”¹åº”ç”¨ä»£ç ï¼‰](#step-24--%E7%BB%99-sa-%E5%8A%A0-irsa-%E6%B3%A8%E8%A7%A3--%E6%B3%A8%E5%85%A5-s3-%E5%8F%98%E9%87%8F--%E5%9B%9E%E6%BB%9A%E6%9B%B4%E6%96%B0%E4%B8%8D%E6%94%B9%E5%BA%94%E7%94%A8%E4%BB%A3%E7%A0%81)
    - [å‡†å¤‡å˜é‡ï¼ˆç”¨ `terraform output` çš„å€¼ï¼‰](#%E5%87%86%E5%A4%87%E5%8F%98%E9%87%8F%E7%94%A8-terraform-output-%E7%9A%84%E5%80%BC)
    - [æ›´æ–° `post-recreate.sh` æ–‡ä»¶](#%E6%9B%B4%E6%96%B0-post-recreatesh-%E6%96%87%E4%BB%B6)
    - [æ³¨å…¥ S3 ç›¸å…³å˜é‡ï¼ˆå¤ç”¨å·²æœ‰çš„ ConfigMap/envFromï¼‰](#%E6%B3%A8%E5%85%A5-s3-%E7%9B%B8%E5%85%B3%E5%8F%98%E9%87%8F%E5%A4%8D%E7%94%A8%E5%B7%B2%E6%9C%89%E7%9A%84-configmapenvfrom)
    - [åº”ç”¨å¹¶æ›´æ–°](#%E5%BA%94%E7%94%A8%E5%B9%B6%E6%9B%B4%E6%96%B0)
    - [åŸºæœ¬è‡ªæ£€](#%E5%9F%BA%E6%9C%AC%E8%87%AA%E6%A3%80)
  - [Step 3/4 â€” é›†ç¾¤å†…ç”¨ aws-cli åš STS/S3 æœ€å°é—­ç¯éªŒè¯ï¼ˆå«è¶Šæƒè¢«æ‹’ï¼‰](#step-34--%E9%9B%86%E7%BE%A4%E5%86%85%E7%94%A8-aws-cli-%E5%81%9A-stss3-%E6%9C%80%E5%B0%8F%E9%97%AD%E7%8E%AF%E9%AA%8C%E8%AF%81%E5%90%AB%E8%B6%8A%E6%9D%83%E8%A2%AB%E6%8B%92)
    - [å†™ Job æ¸…å•](#%E5%86%99-job-%E6%B8%85%E5%8D%95)
    - [è¿è¡Œä¸æŸ¥çœ‹ç»“æœ](#%E8%BF%90%E8%A1%8C%E4%B8%8E%E6%9F%A5%E7%9C%8B%E7%BB%93%E6%9E%9C)
    - [æ›´æ–° `scripts/post-recreate.sh` è„šæœ¬](#%E6%9B%B4%E6%96%B0-scriptspost-recreatesh-%E8%84%9A%E6%9C%AC)
  - [Step 4/4 â€” S3 æˆæœ¬/å®‰å…¨å¢å¼ºï¼ˆGateway Endpoint + Bucket Policy å¼ºåŒ– + å‰ç¼€ç”Ÿå‘½å‘¨æœŸï¼‰](#step-44--s3-%E6%88%90%E6%9C%AC%E5%AE%89%E5%85%A8%E5%A2%9E%E5%BC%BAgateway-endpoint--bucket-policy-%E5%BC%BA%E5%8C%96--%E5%89%8D%E7%BC%80%E7%94%9F%E5%91%BD%E5%91%A8%E6%9C%9F)
    - [åœ¨ç§æœ‰å­ç½‘æ‰“é€šç›´è¿ï¼šVPC Gateway Endpoint for S3ï¼ˆçœ NAT è´¹ï¼‰](#%E5%9C%A8%E7%A7%81%E6%9C%89%E5%AD%90%E7%BD%91%E6%89%93%E9%80%9A%E7%9B%B4%E8%BF%9Evpc-gateway-endpoint-for-s3%E7%9C%81-nat-%E8%B4%B9)
    - [Bucket Policy å¼ºåŒ–ï¼ˆæœ€å°ä¸”ä¸â€œè¯¯æ€â€ç°æœ‰æµç¨‹ï¼‰](#bucket-policy-%E5%BC%BA%E5%8C%96%E6%9C%80%E5%B0%8F%E4%B8%94%E4%B8%8D%E8%AF%AF%E6%9D%80%E7%8E%B0%E6%9C%89%E6%B5%81%E7%A8%8B)
    - [åªæ¸…ç†â€œæµ‹è¯•å‰ç¼€â€çš„ç”Ÿå‘½å‘¨æœŸï¼ˆé¿å…è„æ•°æ®ï¼‰](#%E5%8F%AA%E6%B8%85%E7%90%86%E6%B5%8B%E8%AF%95%E5%89%8D%E7%BC%80%E7%9A%84%E7%94%9F%E5%91%BD%E5%91%A8%E6%9C%9F%E9%81%BF%E5%85%8D%E8%84%8F%E6%95%B0%E6%8D%AE)
    - [å…·ä½“çš„ HCL æ–‡ä»¶æ”¹åŠ¨](#%E5%85%B7%E4%BD%93%E7%9A%84-hcl-%E6%96%87%E4%BB%B6%E6%94%B9%E5%8A%A8-1)
      - [æ›´æ–° `infra/aws/main.tf` æ–‡ä»¶](#%E6%9B%B4%E6%96%B0-infraawsmaintf-%E6%96%87%E4%BB%B6)
      - [æ›´æ–° `infra/aws/modules/app_irsa_s3/main.tf` æ–‡ä»¶](#%E6%9B%B4%E6%96%B0-infraawsmodulesapp_irsa_s3maintf-%E6%96%87%E4%BB%B6)
      - [æ›´æ–° `infra/aws/modules/app_irsa_s3/outputs.tf` æ–‡ä»¶](#%E6%9B%B4%E6%96%B0-infraawsmodulesapp_irsa_s3outputstf-%E6%96%87%E4%BB%B6)
      - [æ›´æ–° `infra/aws/modules/app_irsa_s3/variables.tf` æ–‡ä»¶](#%E6%9B%B4%E6%96%B0-infraawsmodulesapp_irsa_s3variablestf-%E6%96%87%E4%BB%B6)
      - [æ›´æ–° `infra/aws/modules/network_base/main.tf` æ–‡ä»¶](#%E6%9B%B4%E6%96%B0-infraawsmodulesnetwork_basemaintf-%E6%96%87%E4%BB%B6)
      - [æ›´æ–° `infra/aws/modules/network_base/outputs.tf` æ–‡ä»¶](#%E6%9B%B4%E6%96%B0-infraawsmodulesnetwork_baseoutputstf-%E6%96%87%E4%BB%B6)
      - [æ›´æ–° `infra/aws/outputs.tf` æ–‡ä»¶](#%E6%9B%B4%E6%96%B0-infraawsoutputstf-%E6%96%87%E4%BB%B6)

<!-- END doctoc generated TOC please keep comment here to allow auto update -->

# Day 4 - S3 æœ€å°æ¥å…¥ + IRSA

## ç›®æ ‡

**IRSA + S3 æœ€å°é—­ç¯**ï¼šè®© `svc-task/task-api` åœ¨ **ä¸ä½¿ç”¨ä»»ä½•é•¿æœŸå¯†é’¥** çš„å‰æä¸‹ï¼Œå‡­ **ServiceAccount â†’ IRSA â†’ IAM Role** è®¿é—® **S3** çš„æŒ‡å®šå‰ç¼€ï¼Œå¹¶å®Œæˆç«¯åˆ°ç«¯æ ¡éªŒä¸å®‰å…¨/æˆæœ¬åŸºçº¿é…ç½®ã€‚

1. æ–°å»º **S3 bucket**ï¼ˆTerraform æˆ–æ§åˆ¶å°å‡å¯ï¼›**Block Public Access**ã€SSE-S3 å¼€å¯ï¼‰ã€‚
2. é…ç½® **IAM è§’è‰²**ï¼ˆIRSAï¼‰åªå…è®¸ `bucket/$APP/*` å‰ç¼€è¯»å†™ï¼›æŠŠ Role ç»‘å®šåˆ° `${APP}-sa`ã€‚
3. åœ¨åº”ç”¨æ·»åŠ  `/api/files/put?key=...` ä¸ `/api/files/get?key=...` ä¸¤ä¸ªç«¯ç‚¹ï¼Œæ¼”ç¤ºæœ€å°è¯»å†™ã€‚

éªŒæ”¶æ¸…å•ï¼š

- Podï¼ˆä½¿ç”¨ `task-api` çš„ SAï¼‰åœ¨å®¹å™¨å†…ï¼š
  - èƒ½æ‹¿åˆ° STS èº«ä»½ï¼ˆ`aws sts get-caller-identity` æˆåŠŸï¼‰ã€‚
  - èƒ½å¯¹ç›®æ ‡ **S3 æ¡¶/æŒ‡å®šå‰ç¼€** æ‰§è¡Œæœ€å°æ“ä½œï¼ˆ`PutObject/GetObject/List` æˆåŠŸï¼‰ã€‚
  - è®¿é—®ä¸åœ¨æƒé™èŒƒå›´å†…çš„è·¯å¾„æ—¶ **è¢«æ‹’ç»**ï¼ˆéªŒè¯**æœ€å°æƒé™**æœ‰æ•ˆï¼‰ã€‚
- æ‰€æœ‰å˜æ›´å¯éš **`make start-all / stop-all`** è‡ªåŠ¨å¤ç°ï¼ˆTerraform + `post-recreate.sh` å·²å¯¹é½ï¼‰ã€‚

æ„ä¹‰ï¼š

- **éªŒè¯ Pod èƒ½åœ¨å®Œå…¨æ²¡æœ‰é•¿æœŸå¯†é’¥çš„æƒ…å†µä¸‹**ï¼Œé€šè¿‡ **ServiceAccount** â†’ **IRSAï¼ˆEKS OIDCï¼‰** â†’ **IAM Role** â†’ **STS çŸ­æœŸå‡­è¯**è®¿é—®å¤–éƒ¨äº‘èµ„æºï¼ˆä»¥ **S3** ä¸ºä¾‹ï¼‰ã€‚
- åŒæ—¶éªŒè¯**æœ€å°æƒé™**ï¼ˆåªæ”¾è¡Œåˆ° `s3://<bucket>/<app-prefix>/*`ï¼Œå¹¶æŠŠ `ListBucket` é™å®šåˆ°è¯¥å‰ç¼€ï¼‰ï¼Œè¯æ˜â€œèƒ½åŠ›æ”¶æ•›è€Œä¸æ˜¯ä¸€æŠŠæ¢­â€ã€‚

---

## Step 1/4ï¼šTerraform â€” S3 æ¡¶ + IRSAï¼ˆæœ€å°æƒé™åˆ°å‰ç¼€ï¼‰

### éœ€è¦è½å®çš„ä¿®æ”¹ï¼ˆæœ€å°å¿…éœ€é›†ï¼‰

#### S3 Bucketï¼ˆé»˜è®¤å®‰å…¨åŸºçº¿ï¼‰

**èµ„æºï¼š**

1. `aws_s3_bucket`ï¼ˆåç§°å…¨å±€å”¯ä¸€ï¼‰
2. `aws_s3_bucket_public_access_block`ï¼ˆå››é¡¹å…¨å¼€ï¼‰
3. `aws_s3_bucket_ownership_controls`ï¼ˆ`BucketOwnerEnforced`ï¼‰
4. `aws_s3_bucket_server_side_encryption_configuration`ï¼ˆé»˜è®¤ **SSE-S3**ï¼‰
5. `aws_s3_bucket_versioning`ï¼ˆ`Enabled`ï¼‰
6. `aws_s3_bucket_lifecycle_configuration`ï¼šä»…å¯¹æµ‹è¯•å‰ç¼€ï¼ˆ`s3_prefix`ï¼‰åšåˆ°æœŸæ¸…ç†

**å…³é”®ç‚¹ï¼š**

- æ¡¶å**å¿…é¡»å…¨å±€å”¯ä¸€**ã€‚
- é»˜è®¤æ‹’ç»å…¬ç½‘ã€é»˜è®¤åŠ å¯†ï¼›ç”Ÿå‘½å‘¨æœŸåªä½œç”¨åœ¨**æµ‹è¯•å‰ç¼€**ä¸Šï¼Œé¿å…è¯¯åˆ ã€‚

#### IAM Policyï¼ˆä»…å…è®¸è¯¥å‰ç¼€ï¼‰

**èµ„æºï¼š** `aws_iam_policy`ï¼ˆæˆ– inline policyï¼‰

**ç­–ç•¥è¦ç‚¹ï¼ˆä¸¤æ¡ Allowï¼‰ï¼š**

1. **ListBucket**ï¼šåªå…è®¸åˆ—ä¸¾ `s3_prefix`
   - `Action: "s3:ListBucket"`
   - `Resource: arn:aws:s3:::<bucket>`
   - `Condition`ï¼š`StringLike "s3:prefix" = ["${s3_prefix}*", "${s3_prefix}"]`
2. **Get/Put**ï¼šåªå…è®¸å¯¹è±¡åœ¨è¯¥å‰ç¼€
   - `Action: ["s3:GetObject","s3:PutObject"]`
   - `Resource: arn:aws:s3:::<bucket>/${s3_prefix}*`

> å¤‡æ³¨ï¼šæš‚ä¸å¼ºåˆ¶ `DeleteObject`ï¼ŒéªŒè¯é—­ç¯å¤Ÿç”¨ï¼›éœ€è¦æ—¶å•ç‹¬åŠ ã€‚

#### IRSA Roleï¼ˆä¿¡ä»» EKS OIDCï¼Œä»…ç»‘å®šåˆ°åº”ç”¨çº§ SAï¼‰

**èµ„æºï¼š**

1. `aws_iam_role`ï¼ˆä¿¡ä»»ç­–ç•¥ `sts:AssumeRoleWithWebIdentity`ï¼‰
   - `Principal.Federated = <EKS OIDC provider ARN>`
   - `Condition`ï¼š
     - `StringEquals "<OIDC_URL>:aud" = "sts.amazonaws.com"`
     - `StringEquals "<OIDC_URL>:sub" = "system:serviceaccount:${namespace}:${sa_name}"`
2. `aws_iam_role_policy_attachment`ï¼ˆæŠŠä¸Šé¢çš„ S3 policy æŒ‚åˆ°è¯¥ Roleï¼‰

> æ³¨æ„ï¼š`<OIDC_URL>` è¦å»æ‰ `https://` å†æ‹¼ï¼š`replace(var.oidc_provider_url, "https://", "")`ã€‚

### å…·ä½“çš„ HCL æ–‡ä»¶æ”¹åŠ¨

#### ä¿®æ”¹ `infra/aws/main.tf` æ–‡ä»¶

æ–°å¢å†…å®¹ï¼š

```hcl
...

module "task_api" {
  source            = "./modules/app_irsa_s3"                    # åº”ç”¨çº§ S3 æ¡¶ + IRSA æƒé™æ¨¡å—
  create_irsa       = var.create_eks                             # ä»…åœ¨åˆ›å»º EKS æ—¶ç”Ÿæˆ IRSA è§’è‰²ï¼ˆs3 æ¡¶ä¸å—å½±å“ï¼‰
  cluster_name      = var.cluster_name                           # é›†ç¾¤åç§°
  region            = var.region                                 # éƒ¨ç½²åŒºåŸŸ
  namespace         = var.task_api_namespace                     # åº”ç”¨æ‰€åœ¨å‘½åç©ºé—´
  sa_name           = var.task_api_sa_name                       # ç›®æ ‡ ServiceAccount åç§°
  app_name          = var.task_api_app_name                      # åº”ç”¨åç§°
  s3_bucket_name    = var.task_api_s3_bucket_name                # å¯é€‰æŒ‡å®š S3 æ¡¶åç§°
  s3_prefix         = var.task_api_s3_prefix                     # S3 å‰ç¼€
  oidc_provider_arn = module.eks.oidc_provider_arn               # OIDC Provider ARN
  oidc_provider_url = module.eks.oidc_provider_url_without_https # OIDC Provider URLï¼ˆæ—  httpsï¼‰
  depends_on        = [module.eks]                               # ä¾èµ– EKS æ¨¡å—
}

...
```

#### ä¿®æ”¹ `infra/aws/outputs.tf` æ–‡ä»¶

æ–°å¢å†…å®¹ï¼š

```hcl
...

output "task_api_irsa_role_arn" {
  description = "task-api åº”ç”¨ä½¿ç”¨çš„ IRSA Role ARN"
  value       = var.create_eks ? module.task_api.irsa_role_arn : null
}

output "task_api_bucket_name" {
  description = "task-api åº”ç”¨çš„ S3 æ¡¶åç§°"
  value       = module.task_api.bucket_name
}

output "task_api_bucket_arn" {
  description = "task-api åº”ç”¨çš„ S3 æ¡¶ ARN"
  value       = module.task_api.bucket_arn
}

output "task_api_s3_prefix" {
  description = "task-api åº”ç”¨ä½¿ç”¨çš„ S3 å‰ç¼€"
  value       = module.task_api.s3_prefix
}

output "task_api_bucket_url" {
  description = "task-api åº”ç”¨çš„ S3 URL"
  value       = module.task_api.bucket_url
}
```

#### ä¿®æ”¹ `infra/aws/terraform.tfvars` æ–‡ä»¶

æ–°å¢å†…å®¹ï¼š

```hcl
...

# --- task-api åº”ç”¨é…ç½® ---
task_api_namespace      = "svc-task"
task_api_sa_name        = "task-api"
task_api_app_name       = "task-api"
task_api_s3_bucket_name = null
task_api_s3_prefix      = "task-api/"

...
```

#### ä¿®æ”¹ `infra/aws/variables.tf` æ–‡ä»¶

æ–°å¢å†…å®¹ï¼š

```hcl
...

# -------- Task API åº”ç”¨é…ç½® --------
variable "task_api_namespace" {
  description = "Namespace for task API ServiceAccount"
  type        = string
  default     = "svc-task"
}

variable "task_api_sa_name" {
  description = "ServiceAccount name for task API"
  type        = string
  default     = "task-api"
}

variable "task_api_app_name" {
  description = "Application name for task API"
  type        = string
  default     = "task-api"
}

variable "task_api_s3_bucket_name" {
  description = "Optional S3 bucket name for task API"
  type        = string
  default     = null
}

variable "task_api_s3_prefix" {
  description = "S3 prefix for task API objects"
  type        = string
  default     = "task-api/"
}

...
```

#### æ–°å¢ `infra/aws/modules/app_irsa_s3/main.tf` æ–‡ä»¶

```hcl
// ---------------------------
// åº”ç”¨çº§äº‘æƒé™æ¨¡å—ï¼šS3 æ¡¶ + IRSA Role
// ---------------------------

locals {
  bucket_name = coalesce( # è‹¥æœªæŒ‡å®šåˆ™ç”Ÿæˆå”¯ä¸€æ¡¶å
    var.s3_bucket_name,
    "${var.cluster_name}-${var.app_name}-${random_pet.bucket_suffix.id}"
  )
  oidc_url_without_https = var.oidc_provider_url == null ? null : replace(var.oidc_provider_url, "https://", "")
}

resource "random_pet" "bucket_suffix" {
  length    = 2
  separator = "-"
}

# --- S3 Bucket ---
resource "aws_s3_bucket" "this" {
  bucket = local.bucket_name # S3 æ¡¶åç§°

  tags = {
    Application = var.app_name     # æ‰€å±åº”ç”¨
    Environment = var.cluster_name # ç¯å¢ƒ/é›†ç¾¤å
    Region      = var.region       # éƒ¨ç½²åŒºåŸŸ
  }

  lifecycle {
    prevent_destroy = true # é¿å…æ¯æ—¥é”€æ¯æµç¨‹è¯¯åˆ 
  }
}

resource "aws_s3_bucket_public_access_block" "this" {
  bucket                  = aws_s3_bucket.this.id # ç»‘å®šåˆ°ä¸Šæ–¹æ¡¶
  block_public_acls       = true                  # ç¦æ­¢ Public ACL
  block_public_policy     = true                  # ç¦æ­¢ Public Policy
  ignore_public_acls      = true                  # å¿½ç•¥å·²æœ‰çš„ Public ACL
  restrict_public_buckets = true                  # é˜»æ­¢å…¬å…±æ¡¶
}

resource "aws_s3_bucket_ownership_controls" "this" {
  bucket = aws_s3_bucket.this.id

  rule {
    object_ownership = "BucketOwnerEnforced" # ç»Ÿä¸€æ‰€æœ‰æƒè¯­ä¹‰
  }
}

resource "aws_s3_bucket_server_side_encryption_configuration" "this" {
  bucket = aws_s3_bucket.this.id

  rule {
    apply_server_side_encryption_by_default {
      sse_algorithm = "AES256" # é»˜è®¤ä½¿ç”¨ SSE-S3 åŠ å¯†
    }
  }
}

resource "aws_s3_bucket_versioning" "this" {
  bucket = aws_s3_bucket.this.id

  versioning_configuration {
    status = "Enabled" # æ‰“å¼€ç‰ˆæœ¬æ§åˆ¶ä»¥ä¾¿å®¡è®¡å›æ»š
  }
}

resource "aws_s3_bucket_lifecycle_configuration" "this" {
  bucket = aws_s3_bucket.this.id

  rule {
    id     = "cleanup-test-prefix" # ä»…æ¸…ç†æµ‹è¯•å‰ç¼€
    status = "Enabled"

    filter {
      prefix = var.s3_prefix # ä½œç”¨äºæŒ‡å®šå‰ç¼€
    }

    expiration {
      days = 30 # 30 å¤©åè‡ªåŠ¨è¿‡æœŸ
    }
  }
}

# --- IAM Policy ---
resource "aws_iam_policy" "this" {
  count       = var.create_irsa ? 1 : 0 # ä»…åœ¨éœ€è¦ IRSA æ—¶åˆ›å»ºç­–ç•¥
  name        = "${var.cluster_name}-${var.app_name}-s3"
  description = "Minimal S3 access for ${var.app_name}"

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Effect   = "Allow"
        Action   = ["s3:ListBucket"]
        Resource = aws_s3_bucket.this.arn
        Condition = {
          StringLike = {
            "s3:prefix" = ["${var.s3_prefix}*"]
          }
        }
      },
      {
        Effect   = "Allow"
        Action   = ["s3:GetObject", "s3:PutObject"]
        Resource = "${aws_s3_bucket.this.arn}/${var.s3_prefix}*"
      }
    ]
  })
}

# --- IRSA Role ---
resource "aws_iam_role" "this" {
  count       = var.create_irsa ? 1 : 0 # æ§åˆ¶ IRSA è§’è‰²åˆ›å»º
  name        = "${var.cluster_name}-${var.app_name}-irsa"
  description = "IRSA role for ${var.app_name}"

  assume_role_policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Effect = "Allow"
        Action = "sts:AssumeRoleWithWebIdentity"
        Principal = {
          Federated = var.oidc_provider_arn
        }
        Condition = {
          StringEquals = {
            "${local.oidc_url_without_https}:aud" = "sts.amazonaws.com"
            "${local.oidc_url_without_https}:sub" = "system:serviceaccount:${var.namespace}:${var.sa_name}"
          }
        }
      }
    ]
  })

  lifecycle {
    create_before_destroy = true # ç¡®ä¿æ›¿æ¢æ—¶å…ˆå»ºååˆ 
  }
}

resource "aws_iam_role_policy_attachment" "this" {
  count      = var.create_irsa ? 1 : 0 # ä»…åœ¨åˆ›å»º IRSA æ—¶é™„åŠ ç­–ç•¥
  role       = aws_iam_role.this[0].name
  policy_arn = aws_iam_policy.this[0].arn

  lifecycle {
    create_before_destroy = true
  }
}
```

#### æ–°å¢ `infra/aws/modules/app_irsa_s3/outputs.tf` æ–‡ä»¶

```hcl
output "irsa_role_arn" {
  description = "IRSA role ARN"
  value       = try(aws_iam_role.this[0].arn, null)
}

output "bucket_name" {
  description = "S3 bucket name"
  value       = aws_s3_bucket.this.bucket
}

output "bucket_arn" {
  description = "S3 bucket ARN"
  value       = aws_s3_bucket.this.arn
}

output "s3_prefix" {
  description = "S3 prefix for objects"
  value       = var.s3_prefix
}

output "bucket_url" {
  description = "S3 URL with prefix"
  value       = "s3://${aws_s3_bucket.this.bucket}/${var.s3_prefix}"
}
```

#### æ–°å¢ `infra/aws/modules/app_irsa_s3/variables.tf` æ–‡ä»¶

```hcl
variable "cluster_name" {
  description = "EKS cluster name"
  type        = string
}

variable "region" {
  description = "AWS region"
  type        = string
}

variable "namespace" {
  description = "Kubernetes namespace for the ServiceAccount"
  type        = string
}

variable "sa_name" {
  description = "Kubernetes ServiceAccount name"
  type        = string
}

variable "app_name" {
  description = "Application name"
  type        = string
}

variable "s3_bucket_name" {
  description = "S3 bucket name (optional)"
  type        = string
  default     = null
}

variable "s3_prefix" {
  description = "S3 object prefix"
  type        = string
}

variable "oidc_provider_arn" {
  description = "OIDC provider ARN"
  type        = string
  default     = null
}

variable "oidc_provider_url" {
  description = "OIDC provider URL"
  type        = string
  default     = null
}

variable "create_irsa" {
  description = "Whether to create IAM policy and IRSA role"
  type        = bool
  default     = true
}
```

#### æ–°å¢ `infra/aws/modules/app_irsa_s3/versions.tf` æ–‡ä»¶

```hcl
// æ¨¡å—ä½¿ç”¨çš„ Terraform åŠ provider ç‰ˆæœ¬
terraform {
  required_version = "~> 1.12"

  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = "~> 6.0"
    }
    random = {
      source  = "hashicorp/random"
      version = "~> 3.7"
    }
  }
}
```

### Terraform è¾“å‡ºï¼ˆä¾›è„šæœ¬ä¸ K8s ä½¿ç”¨ï¼‰

è¿è¡Œ `terraform apply` åï¼ŒTerraform çš„ Outputs åŒ…å«å¦‚ä¸‹ï¼š

```bash
...
task_api_bucket_arn = "arn:aws:s3:::dev-task-api-welcomed-anteater"
task_api_bucket_name = "dev-task-api-welcomed-anteater"
task_api_bucket_url = "s3://dev-task-api-welcomed-anteater/task-api/"
task_api_irsa_role_arn = "arn:aws:iam::563149051155:role/dev-task-api-irsa"
task_api_s3_prefix = "task-api/"
...
```

### å¿«é€Ÿè‡ªæ£€ï¼ˆCLI å³æ—¶æ ¡éªŒï¼‰

å…ˆè®¾ç½®ï¼š

```bash
export AWS_PROFILE=phase2-sso
export AWS_REGION=us-east-1
```

**IRSA Role å­˜åœ¨ä¸”é™„åŠ äº†ç­–ç•¥**

```bash
$ aws iam get-role --role-name "dev-task-api-irsa" \
  --query 'Role.Arn'

"arn:aws:iam::563149051155:role/dev-task-api-irsa"

$ aws iam list-attached-role-policies --role-name "dev-task-api-irsa" \
  --query 'AttachedPolicies[].PolicyName'

[
    "dev-task-api-s3"
]

```

**ä¿¡ä»»ç­–ç•¥ç»‘å®šåˆ° SA**

```bash
$ aws iam get-role --role-name "dev-task-api-irsa" \
  --query 'Role.AssumeRolePolicyDocument.Statement[0].Condition'

{
    "StringEquals": {
        "oidc.eks.us-east-1.amazonaws.com/id/832372465C509509317C17435FAFD16D:sub": "system:serviceaccount:svc-task:task-api",
        "oidc.eks.us-east-1.amazonaws.com/id/832372465C509509317C17435FAFD16D:aud": "sts.amazonaws.com"
    }
}

# å·²ç»åŒ…å«ï¼š
#   "<OIDC_HOST>:aud": "sts.amazonaws.com"
#   "<OIDC_HOST>:sub": "system:serviceaccount:svc-task:task-api"
```

**S3 æ¡¶å®‰å…¨åŸºçº¿**

```bash
$ aws s3api get-bucket-encryption --bucket "dev-task-api-welcomed-anteater"

{
    "ServerSideEncryptionConfiguration": {
        "Rules": [
            {
                "ApplyServerSideEncryptionByDefault": {
                    "SSEAlgorithm": "AES256"
                },
                "BucketKeyEnabled": false
            }
        ]
    }
}

$ aws s3api get-public-access-block --bucket "dev-task-api-welcomed-anteater"

{
    "PublicAccessBlockConfiguration": {
        "BlockPublicAcls": true,
        "IgnorePublicAcls": true,
        "BlockPublicPolicy": true,
        "RestrictPublicBuckets": true
    }
}

$ aws s3api get-bucket-ownership-controls --bucket "dev-task-api-welcomed-anteater"

{
    "OwnershipControls": {
        "Rules": [
            {
                "ObjectOwnership": "BucketOwnerEnforced"
            }
        ]
    }
}
```

**å‰ç¼€çº¦æŸ**ï¼š

- Role å…³è”çš„ Policy `dev-task-api-s3` çš„ JSON å·²ç»æŠŠèµ„æºé™å®šåˆ° `arn:aws:s3:::dev-task-api-welcomed-anteater/task-api/*`ï¼š
     ```json
     {
         "Statement": [
             {
                 "Action": [
                     "s3:ListBucket"
                 ],
                 "Condition": {
                     "StringLike": {
                         "s3:prefix": [
                             "task-api/*"
                         ]
                     }
                 },
                 "Effect": "Allow",
                 "Resource": "arn:aws:s3:::dev-task-api-welcomed-anteater"
             },
             {
                 "Action": [
                     "s3:GetObject",
                     "s3:PutObject"
                 ],
                 "Effect": "Allow",
                 "Resource": "arn:aws:s3:::dev-task-api-welcomed-anteater/task-api/*"
             }
         ],
         "Version": "2012-10-17"
     }
     ```
- çœŸæ­£åŠŸèƒ½æ€§éªŒè¯ä¼šåœ¨ **Step 3/4** ç”¨ Pod å†… `aws-cli` æ¥æ‰§è¡Œã€‚

---

## Step 2/4 â€” ç»™ SA åŠ  IRSA æ³¨è§£ + æ³¨å…¥ S3 å˜é‡ + å›æ»šæ›´æ–°ï¼ˆä¸æ”¹åº”ç”¨ä»£ç ï¼‰

### å‡†å¤‡å˜é‡ï¼ˆç”¨ `terraform output` çš„å€¼ï¼‰

```bash
export WORK_DIR="/mnt/d/0Repositories/CloudNative"
export AWS_PROFILE=phase2-sso
export AWS_REGION=us-east-1
export NS=svc-task
export APP=task-api
export TASK_API_SERVICE_ACCOUNT_NAME="task-api"

# æ¥è‡ª Terraform è¾“å‡º
export ROLE_ARN="arn:aws:iam::563149051155:role/dev-task-api-irsa"
export S3_BUCKET="dev-task-api-welcomed-anteater"
export S3_PREFIX="task-api/"
```

### æ›´æ–° `post-recreate.sh` æ–‡ä»¶

æ–°å¢å¦‚ä¸‹å†…å®¹ï¼š

```bash
...

# ç¡®ä¿ task-api çš„ ServiceAccount å­˜åœ¨å¹¶å¸¦ IRSA æ³¨è§£
ensure_task_api_service_account() {
  log "ğŸ› ï¸ ç¡®ä¿ task-api ServiceAccount ${TASK_API_SERVICE_ACCOUNT_NAME} å­˜åœ¨å¹¶å¸¦ IRSA æ³¨è§£"
  if ! kubectl -n $NS get sa $TASK_API_SERVICE_ACCOUNT_NAME >/dev/null 2>&1; then
    log "åˆ›å»º ServiceAccount ${TASK_API_SERVICE_ACCOUNT_NAME}"
    kubectl -n ${NS} create serviceaccount ${TASK_API_SERVICE_ACCOUNT_NAME}
  fi
  # å†™å…¥/è¦†ç›– IRSA æ³¨è§£
  kubectl -n ${NS} annotate sa ${TASK_API_SERVICE_ACCOUNT_NAME} \
    "eks.amazonaws.com/role-arn=${TASK_API_ROLE_ARN}" --overwrite
}

# === éƒ¨ç½² task-api åˆ° EKSï¼ˆå¹‚ç­‰ï¼‰===
deploy_task_api() {
  ...
  kubectl -n "${NS}" apply -f "${K8S_BASE_DIR}/ns-sa.yaml"
  # åœ¨è¿™é‡Œè°ƒç”¨ä»è€Œ ç¡®ä¿åº”ç”¨çº§ SA å¸¦ IRSA æ³¨è§£
  ensure_task_api_service_account
  ...
}

...
```

### æ³¨å…¥ S3 ç›¸å…³å˜é‡ï¼ˆå¤ç”¨å·²æœ‰çš„ ConfigMap/envFromï¼‰

> ä¹‹å‰çš„ `ConfigMap task-api-config` å·²ç»é€šè¿‡ `envFrom` æ³¨å…¥åˆ°å®¹å™¨ã€‚
> ç°åœ¨åªéœ€è¦åœ¨ **ConfigMap æ–‡ä»¶** é‡Œå¢åŠ ä¸‰é¡¹ï¼Œç„¶å applyã€‚

ä¿®æ”¹æ–‡ä»¶è·¯å¾„ï¼š`${WORK_DIR}/task-api/k8s/base/configmap.yaml`

å°† `data:` ä¸‹æ–°å¢è¿™ä¸‰è¡Œï¼ˆä¿æŒå…¶å®ƒé”®ä¸å˜ï¼‰ï¼š

```yaml
data:
  APP_NAME: "task-api"
  WELCOME_MSG: "hello from ${AWS_REGION}"
  S3_BUCKET: "dev-task-api-welcomed-anteater"
  S3_PREFIX: "task-api/"
  AWS_REGION: "us-east-1"
```

### åº”ç”¨å¹¶æ›´æ–°

å¦‚æœå·²ç»æ‰§è¡Œäº†æ¯æ—¥é”€æ¯ï¼Œåˆ™å®Œæˆå‰é¢ä¿®æ”¹åï¼Œç›´æ¥é‡å»ºå³å¯è¿›è¡ŒåŸºæœ¬è‡ªæ£€ã€‚

å¦‚æœæ˜¯å¤„äºæ¯æ—¥æ­£å¸¸è¿è¡Œçš„çŠ¶æ€ï¼Œåˆ™æ‰§è¡Œå¦‚ä¸‹å‘½ä»¤æ¥åº”ç”¨ä¿®æ”¹ã€‚

```bash
# ç›´æ¥ annotate
kubectl -n "$NS" annotate sa "$TASK_API_SERVICE_ACCOUNT_NAME" \
  "eks.amazonaws.com/role-arn=$ROLE_ARN" --overwrite

# åº”ç”¨å¹¶æ»šåŠ¨æ›´æ–°
kubectl apply -f "${WORK_DIR}/task-api/k8s/base/configmap.yaml"
kubectl -n "$NS" rollout restart deploy/"$APP"
kubectl -n "$NS" rollout status deploy/"$APP" --timeout=180s
```

### åŸºæœ¬è‡ªæ£€

ç¡®è®¤ IRSA æ³¨å…¥ä¸ç¯å¢ƒå˜é‡å°±ç»ªã€‚

```bash
$ kubectl -n "$NS" get sa "$TASK_API_SERVICE_ACCOUNT_NAME" -o yaml | grep -n "eks.amazonaws.com/role-arn"
# èƒ½çœ‹åˆ°æ³¨è§£é‡Œçš„ Role ARN
5:    eks.amazonaws.com/role-arn: arn:aws:iam::563149051155:role/dev-task-api-irsa

# å–ä¸€ä¸ª Pod åå¹¶æ£€æŸ¥ç¯å¢ƒï¼š
$ POD=$(kubectl -n "$NS" get pods -l app="$TASK_API_SERVICE_ACCOUNT_NAME" -o jsonpath='{.items[0].metadata.name}')

$ kubectl -n "$NS" exec "$POD" -- sh -lc 'env | grep -E "S3_BUCKET|S3_PREFIX|AWS_REGION|AWS_ROLE_ARN|AWS_WEB_IDENTITY_TOKEN_FILE"'
# èƒ½çœ‹åˆ° `AWS_ROLE_ARN` ä¸ `AWS_WEB_IDENTITY_TOKEN_FILE`ï¼ˆç”± EKS Webhook è‡ªåŠ¨æ³¨å…¥ï¼‰
# èƒ½çœ‹åˆ° `S3_BUCKET/S3_PREFIX/AWS_REGION` ä¸‰ä¸ªè‡ªå®šä¹‰å˜é‡
AWS_ROLE_ARN=arn:aws:iam::563149051155:role/dev-task-api-irsa
AWS_WEB_IDENTITY_TOKEN_FILE=/var/run/secrets/eks.amazonaws.com/serviceaccount/token
S3_BUCKET=dev-task-api-welcomed-anteater
WELCOME_MSG=hello from ${AWS_REGION}
AWS_REGION=us-east-1
S3_PREFIX=task-api/

$ kubectl -n "$NS" exec "$POD" -- sh -lc 'ls -l /var/run/secrets/eks.amazonaws.com/serviceaccount/ && [ -s /var/run/secrets/eks.amazonaws.com/serviceaccount/token ] && echo "token OK"'
# ç¡®è®¤ WebIdentity Token å·²æŒ‚è½½
# `token OK` è¡¨ç¤ºæŠ•å½±ä»¤ç‰Œå­˜åœ¨ã€‚
total 0
lrwxrwxrwx 1 root root 12 Aug 25 17:56 token -> ..data/token
token OK
```

---

## Step 3/4 â€” é›†ç¾¤å†…ç”¨ aws-cli åš STS/S3 æœ€å°é—­ç¯éªŒè¯ï¼ˆå«è¶Šæƒè¢«æ‹’ï¼‰

åœ¨é›†ç¾¤é‡Œèµ·ä¸€ä¸ªä¸€æ¬¡æ€§ **aws-cli Job**ï¼Œä½¿ç”¨ `serviceAccountName: task-api`ï¼ˆå·²ç»‘å®š IRSAï¼‰å®Œæˆï¼š

1. å– STS èº«ä»½ï¼›
2. åœ¨**å…è®¸çš„å‰ç¼€**å†™å…¥/åˆ—ä¸¾/è¯»å–ï¼›
3. åœ¨**ä¸å…è®¸çš„å‰ç¼€**å°è¯•å†™å…¥å¹¶ç¡®è®¤è¢«æ‹’ã€‚

### å†™ Job æ¸…å•

> å‰é¢å·²ç»æŠŠ `S3_BUCKET/S3_PREFIX/AWS_REGION` æ”¾åœ¨ `ConfigMap task-api-config`ï¼Œè¿™é‡Œç›´æ¥ `envFrom` å¤ç”¨ã€‚

æ–°å»ºæ–‡ä»¶ï¼š`$WORK_DIR/task-api/k8s/awscli-smoke.yaml`

```yaml
apiVersion: batch/v1
kind: Job
metadata:
  name: awscli-smoke
  namespace: svc-task
spec:
  backoffLimit: 0
  template:
    spec:
      serviceAccountName: task-api
      restartPolicy: Never
      containers:
        - name: awscli
          image: amazon/aws-cli:2.17.33
          imagePullPolicy: IfNotPresent
          envFrom:
            - configMapRef:
                name: task-api-config
          command: ["sh","-lc"]
          args:
            - |
              set -euo pipefail
              echo "== STS get-caller-identity =="
              aws sts get-caller-identity --output json

              TS=$(date +%s)
              KEY_OK="${S3_PREFIX%/}/smoke/${TS}.txt"      # å…è®¸çš„å‰ç¼€
              KEY_DENY="not-allowed/${TS}.txt"             # ä¸å…è®¸çš„å‰ç¼€ï¼ˆåº”è¢«æ‹’ï¼‰
              echo "hello from IRSA $(date -Iseconds)" > /tmp/x.txt

              echo "== Put to allowed prefix =="
              aws s3 cp /tmp/x.txt "s3://${S3_BUCKET}/${KEY_OK}"

              echo "== List allowed prefix (top 5) =="
              aws s3 ls "s3://${S3_BUCKET}/${S3_PREFIX%/}/smoke/" | head -n 5 || true

              echo "== Get just uploaded object =="
              aws s3 cp "s3://${S3_BUCKET}/${KEY_OK}" - | head -c 80; echo

              echo "== Negative test: put to DISALLOWED prefix (should fail) =="
              if aws s3 cp /tmp/x.txt "s3://${S3_BUCKET}/${KEY_DENY}"; then
                echo "UNEXPECTED: write to disallowed prefix succeeded"; exit 2
              else
                echo "EXPECTED: AccessDenied on disallowed prefix"
              fi

              echo "ALL OK"
```

### è¿è¡Œä¸æŸ¥çœ‹ç»“æœ

```bash
$ kubectl apply -f "$WORK_DIR/task-api/k8s/awscli-smoke.yaml"
# è¾“å‡ºï¼š
job.batch/awscli-smoke created

# ç­‰å¾…å®Œæˆï¼ˆæˆåŠŸæˆ–å¤±è´¥éƒ½ä¼šç»“æŸï¼‰
$ kubectl -n svc-task wait --for=condition=complete job/awscli-smoke --timeout=180s || true
# è¾“å‡ºï¼š
job.batch/awscli-smoke condition met

# æŸ¥çœ‹æ—¥å¿—ï¼ˆåº”åŒ…å« STS ä¿¡æ¯ã€Put/List/Get æˆåŠŸï¼Œä»¥åŠå¯¹ not-allowed å‰ç¼€çš„ AccessDeniedï¼‰
$ kubectl -n svc-task logs job/awscli-smoke

# è¾“å‡ºï¼š
# `aws sts get-caller-identity` è¿”å› `Account/Arn`ï¼ˆè¯´æ˜ IRSA ç”Ÿæ•ˆï¼‰
# å…è®¸å‰ç¼€çš„ `cp/ls/cp` æˆåŠŸã€‚
# ä¸å…è®¸å‰ç¼€å†™å…¥æ˜¾ç¤º **AccessDenied** æˆ–ç±»ä¼¼æ‹’ç»ä¿¡æ¯ï¼Œå¹¶æ‰“å° `EXPECTED: AccessDenied`ã€‚

== STS get-caller-identity ==
{
    "UserId": "AROAYGHSMSUJ2PGBBJHBY:botocore-session-1756147736",
    "Account": "563149051155",
    "Arn": "arn:aws:sts::563149051155:assumed-role/dev-task-api-irsa/botocore-session-1756147736"
}
== Put to allowed prefix ==
upload: ../tmp/x.txt to s3://dev-task-api-welcomed-anteater/task-api/smoke/1756147736.txt
== List allowed prefix (top 5) ==
2025-08-25 18:48:58         41 1756147736.txt
== Get just uploaded object ==
hello from IRSA 2025-08-25T18:48:56+0000

== Negative test: put to DISALLOWED prefix (should fail) ==
upload failed: ../tmp/x.txt to s3://dev-task-api-welcomed-anteater/not-allowed/1756147736.txt An error occurred (AccessDenied) when calling the PutObject operation: User: arn:aws:sts::563149051155:assumed-role/dev-task-api-irsa/botocore-session-1756147736 is not authorized to perform: s3:PutObject on resource: "arn:aws:s3:::dev-task-api-welcomed-anteater/not-allowed/1756147736.txt" because no identity-based policy allows the s3:PutObject action
EXPECTED: AccessDenied on disallowed prefix
ALL OK

# éªŒè¯åæ¸…ç†
$ kubectl -n svc-task delete job awscli-smoke --ignore-not-found
# è¾“å‡ºï¼š
job.batch "awscli-smoke" deleted
```

### æ›´æ–° `scripts/post-recreate.sh` è„šæœ¬

æ–°å¢å¦‚ä¸‹å†…å®¹ï¼š

```sh
...

# ---- aws-cli IRSA smoke test ----
# Launches a temporary aws-cli Job (serviceAccount=task-api) to:
#   1) call STS get-caller-identity
#   2) write/list/read under the allowed S3 prefix
#   3) verify writes to a disallowed prefix are denied
awscli_s3_smoke() {
  log "ğŸ§ª aws-cli IRSA S3 smoke test"
  local manifest="${ROOT_DIR}/task-api/k8s/awscli-smoke.yaml"

  kubectl apply -f "$manifest"

  if ! kubectl -n "$NS" wait --for=condition=complete job/awscli-smoke --timeout=180s; then
    kubectl -n "$NS" logs job/awscli-smoke || true
    kubectl -n "$NS" delete job awscli-smoke --ignore-not-found
    abort "aws-cli smoke job failed"
  fi

  kubectl -n "$NS" logs job/awscli-smoke || true
  kubectl -n "$NS" delete job awscli-smoke --ignore-not-found
  log "âœ… aws-cli smoke test finished"
}

# æ£€æŸ¥ task-api
check_task_api() {
  log "ğŸ” éªŒè¯ IRSA æ³¨å…¥ä¸è¿è¡Œæ—¶ç¯å¢ƒ"

  # 1) ServiceAccount æ³¨è§£æ£€æŸ¥
  kubectl -n "${NS}" get sa "${TASK_API_SERVICE_ACCOUNT_NAME}" -o yaml | \
    grep -q "eks.amazonaws.com/role-arn" || \
    abort "ServiceAccount æœªæ­£ç¡®æ³¨è§£ eks.amazonaws.com/role-arn"

  # 2) è·å–ä¸€ä¸ª Pod åç§°ä»¥æ£€æŸ¥ç¯å¢ƒå˜é‡
  local pod
  pod=$(kubectl -n "${NS}" get pods -l app="${TASK_API_SERVICE_ACCOUNT_NAME}" \
    -o jsonpath='{.items[0].metadata.name}' 2>/dev/null || true)
  [[ -z "$pod" ]] && abort "æœªæ‰¾åˆ° ${APP} Podï¼Œæ— æ³•è¿›è¡Œ IRSA è‡ªæ£€"

  # ç­‰å¾… Pod è¿›å…¥ Running çŠ¶æ€
  local wait_time=0
  local max_wait=60
  while [[ $wait_time -lt $max_wait ]]; do
    pod_status=$(kubectl -n "${NS}" get pod "$pod" -o jsonpath='{.status.phase}' 2>/dev/null || echo "Unknown")
    if [[ "$pod_status" == "Running" ]]; then
      break
    fi
    sleep 3
    wait_time=$((wait_time+3))
  done
  [[ "$pod_status" != "Running" ]] && abort "Pod $pod æœªè¿›å…¥ Running çŠ¶æ€ï¼Œå½“å‰çŠ¶æ€: $pod_status"

  # 3) ç¡®è®¤å…³é”®ç¯å¢ƒå˜é‡å­˜åœ¨
  local env_out
  env_out=$(kubectl -n "${NS}" exec "$pod" -- sh -lc 'env') || \
    abort "æ— æ³•è·å– Pod ç¯å¢ƒå˜é‡"
  for key in S3_BUCKET S3_PREFIX AWS_REGION AWS_ROLE_ARN AWS_WEB_IDENTITY_TOKEN_FILE; do
    echo "$env_out" | grep -q "^${key}=" || abort "ç¼ºå°‘ç¯å¢ƒå˜é‡ ${key}"
  done

  # 4) ç¡®è®¤ WebIdentity Token å·²æ­£ç¡®æŒ‚è½½
  kubectl -n "${NS}" exec "$pod" -- sh -lc \
    'ls -l /var/run/secrets/eks.amazonaws.com/serviceaccount/ && [ -s /var/run/secrets/eks.amazonaws.com/serviceaccount/token ]' >/dev/null || \
     abort "WebIdentity Token ç¼ºå¤±æˆ–ä¸ºç©º"

  log "âœ… task-api ServiceAccount IRSA è‡ªæ£€é€šè¿‡"

  log "ğŸ” éªŒè¯ task-api ALBã€Ingressã€dns"

  local outdir="${SCRIPT_DIR}/.out"; mkdir -p "$outdir"
  local dns

  log "â³ Waiting for ALB to be provisioned ..."
  # è·å– ALB çš„ DNS åç§°
  local t=0; local timeout=600
  while [[ $t -lt $timeout ]]; do
    dns=$(kubectl -n "$NS" get ing "$APP" -o jsonpath='{.status.loadBalancer.ingress[0].hostname}' 2>/dev/null || true)
    [[ -n "${dns}" ]] && break
    sleep 5; t=$((t+5))
  done
  [[ -z "${dns}" ]] && abort "Timeout waiting ALB"

  log "âœ… ALB ready: http://${dns}"
  echo "${dns}" > "${outdir}/alb_${APP}_dns"

  log "ğŸ§ª ALB DNS Smoke test: "
  local smoke_retries=10
  local smoke_ok=0
  local smoke_wait=5
  for ((i=1; i<=smoke_retries; i++)); do
    if curl -sf "http://${dns}/api/hello?name=Renda" | sed -n '1p'; then
      smoke_ok=1
      break
    else
      log "â³ Smoke test attempt $i/${smoke_retries} failed, retrying in ${smoke_wait}s..."
      sleep $smoke_wait
    fi
  done
  [[ $smoke_ok -eq 0 ]] && abort "Smoke test failed: /api/hello (DNS may not be ready or network issue)"
  curl -s "http://${dns}/actuator/health" | grep '"status":"UP"' || { log "âŒ Health check failed"; return 1; }

  log "âœ… ALB DNS Smoke test passed"

  awscli_s3_smoke
}

...

check_task_api
```

---

## Step 4/4 â€” S3 æˆæœ¬/å®‰å…¨å¢å¼ºï¼ˆGateway Endpoint + Bucket Policy å¼ºåŒ– + å‰ç¼€ç”Ÿå‘½å‘¨æœŸï¼‰

### åœ¨ç§æœ‰å­ç½‘æ‰“é€šç›´è¿ï¼šVPC Gateway Endpoint for S3ï¼ˆçœ NAT è´¹ï¼‰

1. åœ¨ `infra/aws/` ä¸‹çš„ä½ è®¤ä¸ºåˆé€‚çš„ä½ç½®æˆ–æ–¹å¼æ·»åŠ  **S3 ç½‘å…³ç«¯ç‚¹** å¹¶æŠŠå®ƒ**å…³è”åˆ°æ‰€æœ‰â€œç§æœ‰å­ç½‘â€çš„è·¯ç”±è¡¨**ã€‚
2. æ‰“ä¸Šæ¸…æ™°æ ‡ç­¾ï¼Œä¾¿äºå·¡æ£€ã€‚

**å…³é”®ç»†èŠ‚ï¼š**

- åªæŠŠ**ç§æœ‰å­ç½‘**çš„è·¯ç”±è¡¨å…³è”åˆ° S3 Endpointï¼Œå…¬æœ‰å­ç½‘é€šå¸¸ä¸éœ€è¦ã€‚
- ç«¯ç‚¹å°±ç»ªåï¼Œç§ç½‘é‡Œçš„ EC2/Pod è®¿é—® S3 **ä¸å†èµ° NAT**ï¼Œè®¡è´¹æ˜¾è‘—ä¸‹é™ã€‚

**Terraform è¾“å‡º**

```bash
s3_gateway_endpoint_id = "vpce-00537e89ab3325cf4"
```

**å¿«é€Ÿè‡ªæ£€ï¼š**

```bash
# 1) ç«¯ç‚¹çŠ¶æ€
$ aws ec2 describe-vpc-endpoints --filters "Name=service-name,Values=com.amazonaws.us-east-1.s3" --profile phase2-sso --region us-east-1 \
  --query 'VpcEndpoints[].{Id:VpcEndpointId,State:State,Type:VpcEndpointType,RTs:RouteTableIds}'

# è¾“å‡ºï¼š
[
    {
        "Id": "vpce-00537e89ab3325cf4",
        "State": "available",
        "Type": "Gateway",
        "RTs": [
            "rtb-026a8fa8865c4474c",
            "rtb-00dc799eaa7b2ae78"
        ]
    }
]

# 2) è·¯ç”±è¡¨å‡ºç° S3 å‰ç¼€åˆ—è¡¨è·¯ç”±ï¼ˆpl-å¼€å¤´ï¼‰
$ aws ec2 describe-route-tables \
  --route-table-ids "rtb-026a8fa8865c4474c" "rtb-00dc799eaa7b2ae78" \
  --profile phase2-sso \
  --region us-east-1 \
  --query 'RouteTables[].Routes[?DestinationPrefixListId != `null` && contains(DestinationPrefixListId, `pl-`)]'

# è¾“å‡ºï¼š
[
    [
        {
            "DestinationPrefixListId": "pl-63a5400a",
            "GatewayId": "vpce-00537e89ab3325cf4",
            "Origin": "CreateRoute",
            "State": "active"
        }
    ],
    [
        {
            "DestinationPrefixListId": "pl-63a5400a",
            "GatewayId": "vpce-00537e89ab3325cf4",
            "Origin": "CreateRoute",
            "State": "active"
        }
    ]
]
```

### Bucket Policy å¼ºåŒ–ï¼ˆæœ€å°ä¸”ä¸â€œè¯¯æ€â€ç°æœ‰æµç¨‹ï¼‰

ç»™ `aws_s3_bucket` å¢åŠ ä¸€ä»½ **Bucket Policy**ï¼ˆå¦‚æœå·²ç»æœ‰äº†å°±æ›´æ–°ï¼Œå¦‚æœæ²¡æœ‰å°±æ–°å¢ï¼‰ï¼Œè‡³å°‘åŒ…å«ä¸¤æ¡â€œä¿æŠ¤æ€§ Denyâ€ï¼š

1. **å¼ºåˆ¶ TLS ä¼ è¾“**ï¼ˆä»»ä½•é HTTPS ä¸€å¾‹æ‹’ç»ï¼‰
   - `Effect: Deny`
   - `Principal: "*"`
   - `Action: ["s3:GetObject","s3:PutObject","s3:DeleteObject"]`
   - `Condition: Bool -> aws:SecureTransport = false`
2. **ä»…å…è®¸æ¥è‡ª VPC çš„è®¿é—®**ï¼ˆç» **Gateway Endpoint**ï¼‰
   - `Effect: Deny` / `Principal: "*"`
   - `Action: ["s3:GetObject","s3:PutObject","s3:DeleteObject"]`
   - `Condition: StringNotEqualsIfExists -> aws:SourceVpc = "<VPC ID>"`

> è¯´æ˜ï¼š
>
> - æˆ‘ä»¬**ä¸**åœ¨ Bucket Policy é‡Œå¼ºåˆ¶ `x-amz-server-side-encryption` å¤´ï¼Œå› ä¸ºå·²å¯ç”¨ **é»˜è®¤ SSE-S3**ï¼›å¼ºåˆ¶è¯·æ±‚å¤´ä¼šè®© `aws s3 cp` å¤±è´¥ï¼ˆå®ƒé»˜è®¤ä¸å¸¦å¤´ï¼‰ã€‚
> - ä¸æ¨èç›´æ¥ç”¨ `NotPrincipal` é”æ­»åˆ°å•ä¸€ Roleï¼Œä¼š**å½±å“æ§åˆ¶å°/ä¸´æ—¶è¿ç»´**ï¼›å…ˆç”¨ â€œTLS + SourceVpcâ€ ä¸¤ä¸ªæŠ¤æ å³å¯ã€‚

**Terraform è¾“å‡º**

```bash
task_api_bucket_policy_id = "dev-task-api-welcomed-anteater"
```

**å¿«é€Ÿè‡ªæ£€ï¼š**

```bash
$ aws s3api get-bucket-policy --profile phase2-sso --bucket "de
v-task-api-welcomed-anteater" --query 'Policy' | jq -r .
```

è¾“å‡ºï¼š

```json
{
  "Version": "2012-10-17",
  "Statement": [
    {
      "Sid": "DenyInsecureTransport",
      "Effect": "Deny",
      "Principal": "*",
      "Action": [
        "s3:GetObject",
        "s3:PutObject",
        "s3:DeleteObject"
      ],
      "Resource": [
        "arn:aws:s3:::dev-task-api-welcomed-anteater/*",
        "arn:aws:s3:::dev-task-api-welcomed-anteater"
      ],
      "Condition": {
        "Bool": {
          "aws:SecureTransport": "false"
        }
      }
    },
    {
      "Sid": "DenyNonVpc",
      "Effect": "Deny",
      "Principal": "*",
      "Action": [
        "s3:GetObject",
        "s3:PutObject",
        "s3:DeleteObject"
      ],
      "Resource": [
        "arn:aws:s3:::dev-task-api-welcomed-anteater/*",
        "arn:aws:s3:::dev-task-api-welcomed-anteater"
      ],
      "Condition": {
        "StringNotEqualsIfExists": {
          "aws:SourceVpc": "vpc-0b06ba5bfab99498b"
        }
      }
    }
  ]
}
```

### åªæ¸…ç†â€œæµ‹è¯•å‰ç¼€â€çš„ç”Ÿå‘½å‘¨æœŸï¼ˆé¿å…è„æ•°æ®ï¼‰

åœ¨ `aws_s3_bucket_lifecycle_configuration` å¢åŠ é’ˆå¯¹ **æµ‹è¯•/ä¸´æ—¶** å‰ç¼€çš„è§„åˆ™ï¼Œä¾‹å¦‚ï¼š

- `ID = "cleanup-smoke"`
- `Filter` åªåŒ¹é… `task-api/smoke/`ï¼ˆæˆ–å®é™…ç”¨çš„å‹æµ‹å‰ç¼€ï¼‰
- `Expiration`ï¼š7ï½14 å¤©

**å…³é”®ç»†èŠ‚ï¼š**

- **ä»…**ä½œç”¨äº `smoke/` è¿™ç±»ä¸´æ—¶å‰ç¼€ï¼Œä¸è¦è¦†ç›–ä¸šåŠ¡æ•°æ®ã€‚
- å¦‚æœå¯ç”¨äº† Versioningï¼Œå¯é¢å¤–æ¸…ç† `NoncurrentVersionExpiration`ã€‚

**Terraform è¾“å‡º**

```bash
task_api_bucket_lifecycle_rules = [
  "cleanup-smoke:Enabled",
]
```

**å¿«é€Ÿè‡ªæ£€ï¼š**

```bash
$ aws s3api get-bucket-lifecycle-configuration --profile phase2-sso --bucket "dev-task-api-welcomed-anteater"
```

è¾“å‡ºï¼š

```json
{
    "TransitionDefaultMinimumObjectSize": "all_storage_classes_128K",
    "Rules": [
        {
            "Expiration": {
                "Days": 7
            },
            "ID": "cleanup-smoke",
            "Filter": {
                "Prefix": "task-api/smoke/"
            },
            "Status": "Enabled",
            "NoncurrentVersionExpiration": {
                "NoncurrentDays": 7
            }
        }
    ]
}
```

### å…·ä½“çš„ HCL æ–‡ä»¶æ”¹åŠ¨

#### æ›´æ–° `infra/aws/main.tf` æ–‡ä»¶

åœ¨ task_api æ¨¡å—ä¸­æ–°å¢å¦‚ä¸‹ï¼š

```hcl
...
module "task_api" {
  ...
  # æ–°å¢å¦‚ä¸‹è¡Œï¼Œå…¶ä»–ä¿æŒä¸å˜
  vpc_id            = module.network_base.vpc_id                 # æ¡¶ç­–ç•¥é™åˆ¶è®¿é—®çš„ VPC
  ...
}
...
```

#### æ›´æ–° `infra/aws/modules/app_irsa_s3/main.tf` æ–‡ä»¶

ä¿®æ”¹ `aws_s3_bucket_lifecycle_configuration` èµ„æºä¸ºå¦‚ä¸‹ï¼š

```hcl
...
resource "aws_s3_bucket_lifecycle_configuration" "this" {
  bucket = aws_s3_bucket.this.id

  # ä»…æ¸…ç†ä¸´æ—¶ smoke å‰ç¼€ï¼Œé¿å…è¯¯åˆ ä¸šåŠ¡æ•°æ®
  rule {
    id     = "cleanup-test-prefix" # ä»…æ¸…ç†æµ‹è¯•å‰ç¼€
    id     = "cleanup-smoke"
    status = "Enabled"

    filter {
      prefix = var.s3_prefix # ä½œç”¨äºæŒ‡å®šå‰ç¼€
      prefix = "${var.s3_prefix}smoke/" # ç›®æ ‡å‰ç¼€ï¼š<prefix>smoke/
    }

    expiration {
      days = 30 # 30 å¤©åè‡ªåŠ¨è¿‡æœŸ
      days = 7 # 7 å¤©åè‡ªåŠ¨è¿‡æœŸ
    }

    noncurrent_version_expiration {
      noncurrent_days = 7 # æ¸…ç†æ—§ç‰ˆæœ¬
    }
  }
}
...
```

æ–°å¢å¦‚ä¸‹å†…å®¹ï¼š

```hcl
...

# --- Bucket Policy ---
data "aws_iam_policy_document" "bucket" {
  statement {
    sid    = "DenyInsecureTransport"
    effect = "Deny"
    principals {
      type        = "*"
      identifiers = ["*"]
    }
    # ä»…â€œæ•°æ®é¢â€åŠ¨ä½œï¼š
    # - é¿å…é˜»æ–­ç®¡ç†é¢ï¼ˆGet/PutBucketPolicyã€PutBucketLifecycleConfiguration ç­‰ï¼‰
    # - é¿å…åˆ·æ–°æ—¶ä»å…¬ç½‘ç«¯ç‚¹è¯»å–æ¡¶ä¿¡æ¯è¢« 403
    actions = [
      "s3:GetObject",
      "s3:PutObject",
      "s3:DeleteObject"
    ]
    resources = [
      aws_s3_bucket.this.arn,
      "${aws_s3_bucket.this.arn}/*"
    ]
    condition {
      test     = "Bool"
      variable = "aws:SecureTransport"
      values   = ["false"]
    }
  }

  dynamic "statement" {
    for_each = var.vpc_id == null ? [] : [var.vpc_id]
    content {
      sid    = "DenyNonVpc"
      effect = "Deny"
      principals {
        type        = "*"
        identifiers = ["*"]
      }
      # åŒæ ·åªè¦†ç›–â€œæ•°æ®é¢â€åŠ¨ä½œ
      actions = [
        "s3:GetObject",
        "s3:PutObject",
        "s3:DeleteObject"
      ]
      resources = [
        aws_s3_bucket.this.arn,
        "${aws_s3_bucket.this.arn}/*"
      ]
      condition {
        # IfExists é¿å…åœ¨æ²¡æœ‰ SourceVpc ä¸Šä¸‹æ–‡ï¼ˆä¾‹å¦‚å…¬ç½‘ç«¯ç‚¹ï¼‰æ—¶è¢«è¯¯åˆ¤ä¸ºâ€œä¸ç­‰â€
        test     = "StringNotEqualsIfExists"
        variable = "aws:SourceVpc"
        values   = [var.vpc_id]
      }
    }
  }
}

resource "aws_s3_bucket_policy" "this" {
  bucket = aws_s3_bucket.this.id
  policy = data.aws_iam_policy_document.bucket.json
}

...
```

#### æ›´æ–° `infra/aws/modules/app_irsa_s3/outputs.tf` æ–‡ä»¶

æ–°å¢å¦‚ä¸‹å†…å®¹ï¼š

```hcl
...

output "bucket_policy_id" {
  description = "S3 bucket policy resource ID"
  value       = aws_s3_bucket_policy.this.id
}

output "bucket_lifecycle_rules" {
  description = "Lifecycle rule IDs and statuses"
  value       = [for r in aws_s3_bucket_lifecycle_configuration.this.rule : "${r.id}:${r.status}"]
}
```

#### æ›´æ–° `infra/aws/modules/app_irsa_s3/variables.tf` æ–‡ä»¶

æ–°å¢å¦‚ä¸‹å†…å®¹ï¼š

```hcl
...

variable "vpc_id" {
  description = "VPC ID for optional bucket policy SourceVpc restriction"
  type        = string
  default     = null
}
```

#### æ›´æ–° `infra/aws/modules/network_base/main.tf` æ–‡ä»¶

æ–°å¢å¦‚ä¸‹å†…å®¹ï¼š

```hcl
...
data "aws_region" "current" {}               # å½“å‰åŒºåŸŸï¼Œç”¨äº VPC Endpointï¼ˆä½¿ç”¨ id å­—æ®µï¼‰
...
# S3 Gateway Endpointï¼šç§æœ‰å­ç½‘ç›´è¿ S3ï¼Œç»•è¿‡ NAT ä»¥èŠ‚çœæˆæœ¬
resource "aws_vpc_endpoint" "s3" {
  vpc_id            = aws_vpc.this.id
  service_name      = "com.amazonaws.${data.aws_region.current.id}.s3"
  vpc_endpoint_type = "Gateway"
  route_table_ids   = aws_route_table.private[*].id # ä»…å…³è”ç§æœ‰è·¯ç”±è¡¨

  tags = {
    Name        = "${var.cluster_name}-s3-endpoint"
    ManagedBy   = "Terraform"
    Description = "Gateway endpoint for S3"
  }
}
...
```

#### æ›´æ–° `infra/aws/modules/network_base/outputs.tf` æ–‡ä»¶

æ–°å¢å¦‚ä¸‹å†…å®¹ï¼š

```hcl
...
output "s3_gateway_endpoint_id" {
  description = "S3 ç½‘å…³ç«¯ç‚¹ ID"
  value       = aws_vpc_endpoint.s3.id
}
...
```

#### æ›´æ–° `infra/aws/outputs.tf` æ–‡ä»¶

æ–°å¢å¦‚ä¸‹å†…å®¹ï¼š

```hcl
...
output "s3_gateway_endpoint_id" {
  description = "S3 ç½‘å…³ç«¯ç‚¹ ID"
  value       = module.network_base.s3_gateway_endpoint_id
}
...

output "task_api_bucket_policy_id" {
  description = "task-api æ¡¶ç­–ç•¥èµ„æº ID"
  value       = module.task_api.bucket_policy_id
}

output "task_api_bucket_lifecycle_rules" {
  description = "task-api æ¡¶ç”Ÿå‘½å‘¨æœŸè§„åˆ™åŠçŠ¶æ€"
  value       = module.task_api.bucket_lifecycle_rules
}
```

---
